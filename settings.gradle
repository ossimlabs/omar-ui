pluginManagement {
	repositories {
		mavenLocal()
		maven {
			url ossimMavenProxy
			credentials {
				username ossimMavenProxyUsername
				password ossimMavenProxyPassword
			}
		}
		gradlePluginPortal()
	}
}

rootProject.name='omar-ui'

[ 'lib', 'plugin', 'app' ].each { String projectType ->
	File projectTypeDir = new File( rootDir, "${ projectType }s" )

	if ( projectTypeDir.exists() )
	{
		projectTypeDir.eachDirMatch( { it.endsWith( projectType ) } ) { File subprojectDir ->
			include subprojectDir.name

			project( ":${ subprojectDir.name }" ).projectDir = subprojectDir
		}
	}
}

if ( System.getenv( 'O2_INLINE_BUILD' ) )
{
	def modules = [
           'omar-core',
           'omar-hibernate-spatial',
           'omar-openlayers',
           'omar-security'
        ]

	modules.each { module ->
		include "${ module }-plugin"

		project( ":${ module }-plugin" ).projectDir = "${ rootProject.projectDir }/../${ module }/plugins/${ module }-plugin" as File
	}
}
